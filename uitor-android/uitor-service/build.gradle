apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: "$rootDir/build-android.gradle"
apply from: "$rootDir/build-ktlint.gradle"

ext.artifactId = 'anuitor'

android {
    buildTypes {
        debug {
            buildConfigField "String", "VERSION_CODE", "\"${project["releaseVersion"]}\""
        }
        release {
            buildConfigField "String", "VERSION_CODE", "\"${project["releaseVersion"]}\""
        }
    }
    sourceSets {
        main.java.srcDirs += file("src/extractors")
        //having core extracted for potential bigger in future modularisation
        main.java.srcDirs += file("src/main/core")
    }
    testOptions.unitTests.includeAndroidResources = true
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

dependencies {
    compileOnly "com.google.code.gson:gson:${gson}"
    compileOnly "com.fasterxml.jackson.core:jackson-databind:${jackson}"

    implementation "com.scurab:anuitor-client:${project.releaseVersion}"
    implementation "androidx.annotation:annotation:${androidXAnnotations}"
    //necessary for service
    implementation "androidx.core:core-ktx:${androidXCore}"
    implementation "androidx.fragment:fragment-ktx:${androidXFragments}"
    implementation "io.ktor:ktor-server-cio:$ktor"
    //debug logging
    //implementation("ch.qos.logback:logback-classic:1.2.3")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines"

    compileOnly project(":uitor-service-groovy")
    compileOnly "androidx.recyclerview:recyclerview:${recyclerView}"
    compileOnly "androidx.cardview:cardview:${cardView}"
    compileOnly "androidx.gridlayout:gridlayout:${gridLayout}"
    compileOnly "androidx.percentlayout:percentlayout:${percentLayout}"
    compileOnly "com.google.android.material:material:${androidXMaterial}"
    compileOnly "androidx.viewpager2:viewpager2:${viewPager2}"
    compileOnly "androidx.coordinatorlayout:coordinatorlayout:${coordinatorLayout}"
    compileOnly "androidx.constraintlayout:constraintlayout:${constraintlayout}"
    compileOnly "androidx.navigation:navigation-fragment-ktx:${androidXNavigation}"
    compileOnly "androidx.drawerlayout:drawerlayout:${drawerLayout}"
    compileOnly "androidx.drawerlayout:drawerlayout:${drawerLayout}"
    implementation "androidx.customview:customview:${customView}"


    testImplementation "junit:junit:$junit"
    testImplementation project(":uitor-service-groovy")
    testImplementation "androidx.appcompat:appcompat:$androidXAppCompat"
    //androidTest only, not needed for the lib itself
    testImplementation "com.google.code.gson:gson:${gson}"
    testImplementation "commons-io:commons-io:${commonsIo}"
    testImplementation "org.robolectric:robolectric:${robolectric}"
    testImplementation "org.mockito:mockito-core:${mockito}"
    androidTestImplementation "org.mockito:mockito-core:${mockito}"
}


task createStructureClasses {
    def structure = "${project.projectDir}/src/main/assets/structure.json"
    doLast {
        def targetPackage = 'com.scurab.android.uitor.extract2'
        com.scurab.utior.buildsrc.Script.generateClasses(targetPackage, structure, "${project.projectDir}/src/extractors")
    }
    inputs.files(structure)
    outputs.dir("${project.projectDir}/src/extractors")
    group = "build"
}
preBuild.dependsOn createStructureClasses

task cleanStructureClasses {
    doLast {
        def file = new File("${project.projectDir}/src/extractors")
        println("Delete" + file.deleteDir())
        file.mkdirs()
    }
    group = "build"
}
clean.dependsOn cleanStructureClasses


afterEvaluate {p ->
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = p.artifactId
                artifact tasks.bundleReleaseAar
                artifact tasks.sourcesJar
                //todo jdocs
            }
        }
    }
}
